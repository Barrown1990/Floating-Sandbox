###VERTEX

#version 120

#define in attribute
#define out varying

// Inputs
in vec2 inShipPointTextureCoordinates;
in vec2 inShipPointPosition;
in float inShipPointWater;
in float inShipPointLight;
in float inShipPointPlaneId;
in float inShipPointDecay;
in float inShipPointTemperature;

// Outputs        
out float vertexLight;
out float vertexWater;
out float vertexDecay;
out vec2 vertexTextureCoords;
out float vertexTemperature;

// Params
uniform mat4 paramOrthoMatrix;

void main()
{            
    vertexLight = inShipPointLight;
    vertexWater = inShipPointWater;
    vertexDecay = inShipPointDecay;
    vertexTextureCoords = inShipPointTextureCoordinates;
    vertexTemperature = inShipPointTemperature;

    gl_Position = paramOrthoMatrix * vec4(inShipPointPosition, inShipPointPlaneId, 1.0);
}

###FRAGMENT

#version 120

#include "heat_overlay.glslinc"

#define in varying

// Inputs from previous shader        
in float vertexLight;
in float vertexWater;
in float vertexDecay;
in vec2 vertexTextureCoords;
in float vertexTemperature;

// Params
uniform float paramAmbientLightIntensity;
uniform vec4 paramWaterColor;
uniform float paramWaterContrast;
uniform float paramWaterLevelThreshold;
uniform float paramHeatOverlayTransparency;

// Input texture
uniform sampler2D paramSharedTexture;

void main()
{
    vec4 vertexCol = texture2D(paramSharedTexture, vertexTextureCoords);

    // Discard transparent pixels, so that ropes (which are drawn temporally after
    // this shader but Z-ally behind) are not occluded by transparent triangles
    if (vertexCol.w < 0.2)
        discard;

    // Apply decay
    float originalLightness = (vertexCol.x + vertexCol.y + vertexCol.z) / 3.0;
    vec4 decayColor = mix(vec4(%ROT_GREEN_COLOR%), vec4(%ROT_BROWN_COLOR%), originalLightness);
    vertexCol = mix(decayColor, vertexCol, vertexDecay);

    // Apply point water
    float vertexColorWetness = min(vertexWater, paramWaterLevelThreshold) / paramWaterLevelThreshold * paramWaterContrast;
    vec4 fragColour = mix(vertexCol, paramWaterColor, vertexColorWetness);

    // Complement missing ambient light with point's light
    float totalLightIntensity = paramAmbientLightIntensity + (1.0 - paramAmbientLightIntensity) * vertexLight;

    // Apply light
    fragColour *= totalLightIntensity;

    // Apply heat
    fragColour = apply_heat(fragColour, vertexTemperature, paramHeatOverlayTransparency);

    // Apply point light color
    fragColour = mix(fragColour, vec4(%LAMPLIGHT_COLOR_VEC4%), vertexLight);
    
    gl_FragColor = vec4(fragColour.xyz, vertexCol.w);
} 
