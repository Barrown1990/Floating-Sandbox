###VERTEX

#version 120

#define in attribute
#define out varying

// Inputs
in vec4 inShipPointColor;
in vec2 inShipPointPosition;
in float inShipPointWater;
in float inShipPointLight;
in float inShipPointPlaneId;
in float inShipPointDecay;

// Outputs        
out vec3 vertexAttributes; // Light, Water, Decay
out vec4 vertexCol;

// Params
uniform mat4 paramOrthoMatrix;

void main()
{            
    vertexAttributes = vec3(inShipPointLight, inShipPointWater, inShipPointDecay);
    vertexCol = inShipPointColor;

    gl_Position = paramOrthoMatrix * vec4(inShipPointPosition, inShipPointPlaneId, 1.0);
}

###FRAGMENT

#version 120

#define in varying

// Inputs from previous shader        
in vec3 vertexAttributes; // Light, Water, Decay
in vec4 vertexCol;

// Params
uniform float paramAmbientLightIntensity;
uniform vec4 paramWaterColor;
uniform float paramWaterContrast;
uniform float paramWaterLevelThreshold;

void main()
{
    // Apply decay
    float originalLightness = dot(vec3(1.0/3.0), vertexCol.xyz);
    vec4 decayColor = mix(vec4(%ROT_GREEN_COLOR%), vec4(%ROT_BROWN_COLOR%), originalLightness);
    vec4 fragColour = mix(decayColor, vertexCol, vertexAttributes.z);

    // Apply point water
    float vertexColorWetness = min(vertexAttributes.y, paramWaterLevelThreshold) / paramWaterLevelThreshold * paramWaterContrast;
    fragColour = mix(fragColour, paramWaterColor, vertexColorWetness);

    // Complement missing ambient light with point's light
    float totalLightIntensity = mix(vertexAttributes.x, 1.0, paramAmbientLightIntensity);

    // Apply light
    fragColour *= totalLightIntensity;

    // Apply point light color
    fragColour = mix(fragColour, vec4(%LAMPLIGHT_COLOR_VEC4%), vertexAttributes.x);
    
    gl_FragColor = vec4(fragColour.xyz, vertexCol.w);
} 

